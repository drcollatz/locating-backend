buildscript {
    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.3.RELEASE")
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
        classpath 'org.asciidoctor:asciidoctorj-epub3:1.5.0-alpha.6'
        classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.11'
    }
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'org.asciidoctor.convert'

jar {
    baseName = 'locating-backend'
    version = '0.1.0'
}


mainClassName = "com.fh.locating.Application"

repositories {
    mavenCentral()
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

dependencies {
    compile("org.springframework.boot:spring-boot-starter-data-rest")
    compile("org.springframework.boot:spring-boot-starter-data-mongodb")
    compile 'org.springframework.data:spring-data-rest-hal-browser'
    
    // http://mvnrepository.com/artifact/joda-time/joda-time
	compile group: 'joda-time', name: 'joda-time', version: '2.9.3'
	compile("com.fasterxml.jackson.datatype:jackson-datatype-joda:2.4.2");
	
	compile("org.springframework.boot:spring-boot-starter-test")
	testCompile 'org.springframework.restdocs:spring-restdocs-mockmvc:1.0.1.RELEASE'
	testCompile("junit:junit")
    //testCompile group: "cz.jirutka.spring", name: "embedmongo-spring", version: "1.3.0"
    testCompile group: "de.flapdoodle.embed", name: "de.flapdoodle.embed.mongo", version: "1.26"
    
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

task stage(type: Copy, dependsOn: [clean, build]) {
    from jar.archivePath
    into project.rootDir 
    rename {
        'app.jar'
    }
}
stage.mustRunAfter(clean)

clean << {
    project.file('app.jar').delete()
}

ext { 
    snippetsDir = file('src/docs/generated-snippets')
}

test { 
    outputs.dir snippetsDir
}

task cleanTempDir(type: Delete) {
    delete fileTree(dir: 'src/main/resources/public')
    delete fileTree(dir: 'src/docs/generated-snippets')
}

clean {
    dependsOn cleanTempDir
}

build {
    dependsOn asciidoctor
}

asciidoctor.outputs.upToDateWhen { false }


asciidoctor {
    dependsOn test
    sourceDir = file('src/docs')
    outputDir "$projectDir/src/main/resources/public/docs"
    inputs.dir snippetsDir
    backends 'html5', 'pdf'
    attributes 'source-highlighter' : 'prettify',
            'imagesdir':'images',
            'icons': 'font',
            'setanchors':'true',
            'idprefix':'',
            'idseparator':'-',
            'docinfo1':'true',
            'snippets': snippetsDir
}
